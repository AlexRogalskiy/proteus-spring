buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.2'
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.8'
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.16'
    }
}

plugins {
    id 'java'
    id 'com.gradle.build-scan' version '1.14'
    id 'com.github.hierynomus.license' version '0.14.0'
    id 'com.jfrog.artifactory' version '4.7.3'
    id 'com.jfrog.bintray' version '1.8.4'
}

apply from: "gradle/doc.gradle"

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
    publishAlways()
}

allprojects {
    apply plugin: 'com.github.hierynomus.license'
    apply plugin: 'idea'

    ext {
        proteusVersion = '1.5.0-RC'
        protobufVersion = '3.6.1'
        rsocketRpcVersion = '0.2.4'
        springBootDependenciesVersion = '2.0.5.RELEASE'
    }

    repositories {
        jcenter()
        maven {
            url  "https://dl.bintray.com/netifi/netifi-oss"
        }
        maven {
            url  "https://dl.bintray.com/rsocket-admin/RSocket"
        }
        maven {
            url = version.contains('SNAPSHOT') ? 'https://artifactory.netifiinc.com/artifactory/libs-snapshot-local' : 'https://artifactory.netifiinc.com/artifactory/libs-release-local'
            credentials {
                username = project.property('netifiArtifactoryUsername')
                password = project.property('netifiArtifactoryPassword')
            }
        }
    }

    license {
        header rootProject.file('codequality/HEADER')
        strictCheck true
    }

    test {
        testLogging {
            events "passed", "skipped", "failed"
        }
        useJUnitPlatform()
    }
}