plugins {
    id 'io.spring.dependency-management' version '1.0.7.RELEASE'
    id 'maven-publish'
}

ext.description = "Netifi Proteus Spring Dependencies"
ext.artifactName = 'proteus-spring-boot-dependencies'

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

apply from: "${project.rootProject.projectDir}/gradle/java.gradle"
apply from: "${project.rootProject.projectDir}/artifactory.gradle"
apply from: "${project.rootProject.projectDir}/bintray.gradle"

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootDependenciesVersion}"
        mavenBom "io.netifi.proteus:proteus-bom:${proteusVersion}"
    }
    dependencies {
        dependency "org.springframework.boot:spring-boot-starter:${springBootDependenciesVersion}"
        dependency "io.netifi.proteus:proteus-spring-core:${project.version}"
        dependency "io.netifi.proteus:proteus-spring-boot-autoconfigure:${project.version}"
    }
}

dependencies {
    compile project(':proteus-spring-boot-autoconfigure')
    compile project(':proteus-spring-core')
}

configurations.archives.artifacts.clear()

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = 'Netifi Proteus Spring Dependencies'
                description = 'A bill of materials for Netifi Spring applications'
                url = 'https://www.netifi.com'

                organization {
                    name = 'Netifi, Inc.'
                    url = 'https://www.netifi.com'
                }

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                scm {
                    url = 'https://github.com/netifi-proteus/proteus-spring'
                    connection = 'scm:git:git://github.com/netifi-proteus/proteus-spring'
                    developerConnection = 'scm:git:git://github.com/netifi-proteus/proteus-spring'
                }

                issueManagement {
                    system = "GitHub Issues"
                    url = "https://github.com/netifi-proteus/proteus-spring/issues"
                }
            }
        }
    }
}

publish.dependsOn("build")